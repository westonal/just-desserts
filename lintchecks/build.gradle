apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.android.lint'
apply plugin: 'maven-publish'
apply plugin: 'signing'

lintOptions {
    htmlReport true
    htmlOutput file("lint-report.html")
    textReport true
    absolutePaths false
    ignoreTestSources true
}

dependencies {
    compileOnly "com.android.tools.lint:lint-api:$lintVersion"
    compileOnly "com.android.tools.lint:lint-checks:$lintVersion"

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.android.tools.lint:lint:$lintVersion"
    testImplementation "com.android.tools.lint:lint-tests:$lintVersion"
}

group 'io.github.westonal'
version '0.14'

java {
    withJavadocJar()
    withSourcesJar()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'no-desserts'
            version = version

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'No Desserts Linter'
                description = 'Enforces zero usages of VERSION_CODE constants'
                url = 'https://github.com/westonal/no-desserts'
                licenses {
                    license {
                        name = 'GNU General Public License v3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'westonal'
                        name = 'Alan Evans'
                        email = 'thealanevans@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/westonal/no-desserts.git'
                    developerConnection = 'scm:git:ssh://github.com/westonal/no-desserts.git'
                    url = 'https://github.com/westonal/no-desserts'
                }
            }
        }
    }
    repositories {
        maven {
            def stagingRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def staging = !version.endsWith('SNAPSHOT')
            url = staging ? stagingRepoUrl : snapshotsRepoUrl
            tasks.create('displayPublishLocation') {
                doLast {
                    println("Publishing $version to${staging ? " staging" : ""} $url")
                    if (staging) {
                        println("You will need to promote to a release at https://s01.oss.sonatype.org/#stagingRepositories")
                    }
                }
            }
            credentials {
                // defined in the global local gradle.properties
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

afterEvaluate {
  publish.dependsOn(displayPublishLocation)
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

